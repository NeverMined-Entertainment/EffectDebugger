plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT"
    implementation "dev.jorel:commandapi-bukkit-shade:9.5.3"

    implementation "com.mojang:brigadier:1.0.18"
    implementation("dev.triumphteam:triumph-gui:3.1.10") {
        exclude group: "net.kyori"
    }
    implementation "com.google.inject:guice:7.0.0"

    implementation "me.wyne:WUtils-config:2.6.4"
    implementation "me.wyne:WUtils-configurables:1.0.1"
    implementation "me.wyne:WUtils-log:3.0.5"
    implementation("me.wyne:WUtils-i18n:2.3.9") {
        exclude group: "net.kyori"
    }
}

shadowJar {
    archiveBaseName.set("EffectDebugger")
    archiveClassifier.set("")
    def outputDir = project.hasProperty('shadowJarOutputDir') ? project.shadowJarOutputDir : "${buildDir}/libs"
    destinationDirectory = file(outputDir)
    minimize()
    relocate "dev.jorel.commandapi", "org.nevermined.effectdebugger.shadow.commandapi"
    //relocate "com.mojang.brigadier", "org.nevermined.effectdebugger.shadow.mojang.brigadier"
    relocate "dev.triumphteam.gui", "org.nevermined.effectdebugger.shadow.triumphteam.gui"
    relocate "com.google.inject", "org.nevermined.effectdebugger.shadow.google.guice"
    relocate "com.google.common", "org.nevermined.effectdebugger.shadow.google.common"
    relocate "me.wyne.wutils", "org.nevermined.effectdebugger.shadow.wutils"
}

def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
